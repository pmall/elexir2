#!/usr/bin/perl
use strict;
use warnings;
use YAML::XS qw(LoadFile);
use DBI;
use List::MoreUtils qw(uniq);
use FindBin qw($Bin);
use lib $FindBin::Bin . '/../lib';
use Format;
use Math;
use Utils;
use Stats;
use Replicat;
use Analyse;

# ==============================================================================
# Parametres
# ==============================================================================

# On récupère l'id du projet a analyser
my $id_analyse = shift @ARGV || die('Vous devez passer l\'id d\'une analyse en paramètre');

# On récupère un hash de config
my $config = LoadFile($FindBin::Bin . '/../config.yml');

# On défini les identifiants pour la bdd
my $db_host = $config->{'db_host'};
my $db_user = $config->{'db_user'};
my $db_pass = $config->{'db_pass'};
my $db_name = $config->{'db_name'};

# Parametres pour l'analyse
my $nb_sondes_min_gene = $config->{'simple'}{'nb_sondes_min_gene'};

my $gc_max = $config->{'jonction'}{'gc_max'};
my $nb_occ_max = $config->{'jonction'}{'nb_occ_max'};
my $seuil_dabg = $config->{'jonction'}{'seuil_dabg'};
my $nb_sondes_min_entite = $config->{'jonction'}{'nb_sondes_min_entite'};

# ==============================================================================
# Préparation des requètes pour les infos du projet
# ==============================================================================

# On se connecte a elexir
my $dbh = DBI->connect(
	'DBI:mysql:' . $db_name . ':' . $db_host,
	$db_user,
	$db_pass
);

# On récupère les infos de l'analyse
my $analyse = Analyse->get_analyse($dbh, $id_analyse);

# Message d'erreur si l'analyse n'existe pas
die('L\'id de l\'analyse n\'est pas valide.') if(!$analyse);

# Message d'erreur si c'est pas le bon script pour le bon type d'analyse
if($analyse->{'type'} ne 'jonction'){

	die('Le script jonction ne fonctionne que sur les analyses jonction :o)');

}

# ==============================================================================
# On crée les noms de table dont on a besoin
# ==============================================================================

my $id_projet = $analyse->{'id_project'};
my $type_chips = $analyse->{'type_chips'};
my $version = $analyse->{'version'};
my $orga = $analyse->{'organism'};
my $paire = $analyse->{'paired'};
my $nb_paires_rep = $analyse->{'nb_paires_rep'};

my $table_genes = $config->{'table_genes'}{$version}{$orga};
my $table_entites_a_priori = $config->{'table_entites_a_priori'}{$version}{$orga};
my $table_sondes_exon = $config->{'table_sondes'}{$type_chips}{$version}{$orga};
my $table_sondes_jonctions = $config->{'table_sondes_jonction'}{$type_chips}{$version}{$orga};
my $table_dabg = get_table_dabg($id_projet);
my $table_intensites = get_table_intensites($id_projet);
my $table_trans = get_table_transcription($id_projet, $id_analyse);
my $table_jonction = get_table_jonction($id_projet, $id_analyse);

# ==============================================================================
# On prépare les requete sur elexir pour l'analyse avec ces infos
# ==============================================================================

# On selectionne les gènes
my $select_genes_sth = $dbh->prepare(
	"SELECT id_fasterdb FROM " . $table_genes
);

# On selectionne les entités exoniques d'un gène
my $select_entites_a_priori_sth = $dbh->prepare(
	"SELECT id
	FROM " . $table_entites_a_priori . "
	WHERE id_gene = ?"
);

# On selectionne les sondes exon d'un gène (qui ciblent des exons)
my $select_infos_sondes_exons_sth = $dbh->prepare(
	"SELECT probe_id, nb_occ, gc_content, exon_pos
	FROM " . $table_sondes_exon . "
	WHERE entite_type = 1
	AND utiliser_dans_analyse = 1
	AND gene_id = ?"
);

# On selectionne les sondes jonction de l'événement
my $select_infos_sondes_jonctions_sth = $dbh->prepare(
	"SELECT id_probe, gc_content, nb_occ, groupe
	FROM " . $table_sondes_jonctions . "
	WHERE id_entite = ?"
);

# On selectionne le dabg d'une sonde
my $select_dabg_sth = $dbh->prepare(
	"SELECT * FROM " . $table_dabg . " WHERE probe_id = ?"
);

# On selectionne l'intensité d'une sonde
my $select_intensite_sth = $dbh->prepare(
	"SELECT * FROM " . $table_intensites . " WHERE probe_id = ?"
);

# ==============================================================================
# On crée dynamiquement la table
# ==============================================================================

my @fields_expr = ();
my @fields_SIs = ();
my @fields_SIsds = ();

for(my $i = 0; $i < $analyse->{'nb_paires_rep'}; $i++){

	push(@fields_expr, 'control' . ($i + 1));
	push(@fields_expr, 'test' . ($i + 1));
	push(@fields_SIs, 'SI' . ($i + 1));

}

my @fields_splicing = ();

push(@fields_splicing, 'id_entite INT UNSIGNED NOT NULL PRIMARY KEY');
push(@fields_splicing, 'id_gene INT UNSIGNED NOT NULL');
push(@fields_splicing, 'fc_gene DECIMAL(10,5) UNSIGNED');
push(@fields_splicing, (map { $_ . ' DECIMAL(10,5) UNSIGNED NOT NULL' } @fields_SIs)) if($paire);
push(@fields_splicing, 'SI DECIMAL(10,5) UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_pval DECIMAL(12,10) UNSIGNED NOT NULL');
push(@fields_splicing, 'nb_sondes_inclusion SMALLINT UNSIGNED NOT NULL');
push(@fields_splicing, 'nb_sondes_exclusion SMALLINT UNSIGNED NOT NULL');

$dbh->do('DROP TABLE IF EXISTS ' . $table_jonction);
$dbh->do('CREATE TABLE ' . $table_jonction . ' (' . join(', ', @fields_splicing) . ')');

my $insert_entite_sth = $dbh->prepare(
	"INSERT INTO $table_jonction VALUES(" . join(', ', ('?')x@fields_splicing) . ")"
);

# ==============================================================================
# Début de l'algo, on analyse les gènes avec un fc épissage
# ==============================================================================

# On selectionne les gènes
$select_genes_sth->execute;

# Pour chaque gène
while(my($id_gene) = $select_genes_sth->fetchrow_array){

	# On analyse les entités à priori du gène
	my @entites = ();

	$select_entites_a_priori_sth->execute($id_gene);

	# Pour chaque événement
	while(my $entite = $select_entites_a_priori_sth->fetchrow_hashref){

		# On sélectionne ses sondes jonctions
		$select_infos_sondes_jonctions_sth->execute($entite->{'id'});
		my $infos_sondes_jonctions = $select_infos_sondes_jonctions_sth->fetchall_hashref('id_probe'); 
		$select_infos_sondes_jonctions_sth->finish;

		# Si on a pas 3 sondes on zappe 
		next if(scalar(keys %{$infos_sondes_jonctions}) < $nb_sondes_min_entite);

		# On vire les sondes qui ont pas les bonnes caractéristiques
		# et on sépare les inclusions et exclusions
		my $ids_sondes_inclusion_ok = [grep {
			$infos_sondes_jonctions->{$_}->{'gc_content'} > 2
			and
			$infos_sondes_jonctions->{$_}->{'gc_content'} <= $gc_max
			and
			$infos_sondes_jonctions->{$_}->{'nb_occ'} <= $nb_occ_max
			and
			$infos_sondes_jonctions->{$_}->{'groupe'} eq 'inclusion'
		} keys %{$infos_sondes_jonctions}];

		my $ids_sondes_exclusion_ok = [grep {
			$infos_sondes_jonctions->{$_}->{'gc_content'} > 2
			and
			$infos_sondes_jonctions->{$_}->{'gc_content'} <= $gc_max
			and
			$infos_sondes_jonctions->{$_}->{'nb_occ'} <= $nb_occ_max
			and 
			$infos_sondes_jonctions->{$_}->{'groupe'} eq 'exclusion'
		} keys %{$infos_sondes_jonctions}];

		# On récupère le nombre de sondes inclusion/exclusion
		my $nb_sondes_inclusion_ok = scalar @{$ids_sondes_inclusion_ok};
		my $nb_sondes_exclusion_ok = scalar @{$ids_sondes_exclusion_ok};

		# Si il y a pas 3 sondes inclusion/exclusion on zappe
		next if($nb_sondes_inclusion_ok < $nb_sondes_min_entite);
		next if($nb_sondes_exclusion_ok < $nb_sondes_min_entite);

		# On récupère les infos de toutes les sondes exprimées
		my $ref_sondes_exclusion = $analyse->get_sondes_exprimees(
			$dbh,
			$ids_sondes_exclusion_ok,
			$seuil_dabg
		);

		my $ref_sondes_inclusion = $analyse->get_sondes_exprimees(
			$dbh,
			$ids_sondes_inclusion_ok,
			$seuil_dabg
		);

		# On récupère le nombre de sondes inclusion/exclusion exprimées
		my $nb_sondes_inclusion = scalar @{$ref_sondes_inclusion};
		my $nb_sondes_exclusion = scalar @{$ref_sondes_exclusion};

		# Si il n'y a pas trois sondes inclusion/exclusion exprimées on zappe
		next if($nb_sondes_inclusion < $nb_sondes_min_entite);
		next if($nb_sondes_exclusion < $nb_sondes_min_entite);

		# ==============================================================
		# On calcule le FC des sondes
		# ==============================================================

		# On récupère les FCs des sondes
		my $ref_fcs_inclusion = $analyse->fcs_sondes($ref_sondes_inclusion);
		my $ref_fcs_exclusion = $analyse->fcs_sondes($ref_sondes_exclusion);

		# ==============================================================
		# On calcule le SI des sondes et le SI de l'entité
		# ==============================================================

		my $ref_SIs_sondes = $analyse->sis_sondes(
			rep_effect($ref_fcs_exclusion),
			$ref_fcs_inclusion
		);

		my($SI, $pvalue, @SIs) = $analyse->si_entite($ref_SIs_sondes);

		# ==============================================================
		# On ajoute l'entité à la liste
		# ==============================================================

		push(@entites, {
			'id' => $entite->{'id'},
			'SIs' => \@SIs,
			'SI' => $SI,
			'pvalue' => $pvalue,
			'nb_sondes_inclusion' => $nb_sondes_inclusion,
			'nb_sondes_exclusion' => $nb_sondes_exclusion
		});

	}

	$select_entites_a_priori_sth->finish;

	# Si il n'y a pas d'entités exprimées pour ce gène on zappe
	next if(@entites == 0);

	my $fc_gene;

	# On selectionne les sondes du gène
	$select_infos_sondes_exons_sth->execute($id_gene);
	my $infos_sondes_exons = $select_infos_sondes_exons_sth->fetchall_hashref('probe_id'); 
	$select_infos_sondes_exons_sth->finish;

	# Si le gène a au moins 6 sondes on continue
	if(scalar(keys %{$infos_sondes_exons}) >= $nb_sondes_min_gene){

	# On vire les sondes qui ont pas les bonnes caractéristiques
	my $ids_probes_ok = [grep {
		# ON VIRE LES SONDES AVEC UN GC <= 2 PARCE QU'ON A PAS PU
		# CALCULER LEUR DABG AVEC APT :((
		$infos_sondes_exons->{$_}->{'gc_content'} > 2
		and
		$infos_sondes_exons->{$_}->{'nb_occ'} <= $nb_occ_max
		and
		$infos_sondes_exons->{$_}->{'gc_content'} <= $gc_max
	} keys %{$infos_sondes_exons}];

	# Si le gène a au moins 6 sondes avec de bon critères on continue
	if(@{$ids_probes_ok} >= $nb_sondes_min_gene){

	# On récupère les sondes exprimées
	my $ref_sondes = $analyse->get_sondes_exprimees(
		$dbh,
		$ids_probes_ok,
		$seuil_dabg
	);

	# Si le gène a au moins 6 sondes exprimées on continue
	if(@{$ref_sondes} >= $nb_sondes_min_gene){

	# On fait un lissage transcriptionnel
	my $ref_sondes_trans = $analyse->lissage_transcription($ref_sondes);

	# Si le gène a au moins 6 sondes qui passent le lissage on continue
	if(@{$ref_sondes_trans} >= $nb_sondes_min_gene){

	# Calcul du fc du gène
	($fc_gene) = sum_no_rep_effect($analyse->fcs_sondes($ref_sondes_trans));

	}}}} # On ferme tout

	# On ajoute les entités
	foreach my $entite (@entites){

		my @values = ();

		push(@values, $entite->{'id'});
		push(@values, $id_gene);
		push(@values, $fc_gene);
		push(@values, @{$entite->{'SIs'}}) if($paire);
		push(@values, $entite->{'SI'});
		push(@values, $entite->{'pvalue'});
		push(@values, $entite->{'nb_sondes_inclusion'});
		push(@values, $entite->{'nb_sondes_exclusion'});

		$insert_entite_sth->execute(@values);

	}

}
