#!/usr/bin/perl
use strict;
use warnings;
use YAML::XS qw(LoadFile);
use DBI;
use List::MoreUtils qw(uniq);
use FindBin qw($Bin);
use lib $FindBin::Bin . '/../lib';
use Format;
use Math;
use Analyse;
use Stats;

# ==============================================================================
# Parametres
# ==============================================================================

# On récupère l'id du projet a analyser
my $id_analyse = shift @ARGV || die('Vous devez passer l\'id d\'une analyse en paramètre');

# On récupère un hash de config
my $config = LoadFile($FindBin::Bin . '/../config.yml');

# On défini les identifiants pour la bdd
my $db_host = $config->{'db_host'};
my $db_user = $config->{'db_user'};
my $db_pass = $config->{'db_pass'};
my $db_name = $config->{'db_name'};

# Parametres pour l'analyse
my $gc_max = $config->{'simple'}{'gc_max'};
my $nb_occ_max = $config->{'simple'}{'nb_occ_max'};
my $seuil_dabg = $config->{'simple'}{'seuil_dabg'};
my $seuil_si = $config->{'simple'}{'seuil_si'};

# ==============================================================================
# Préparation des requètes pour les infos du projet
# ==============================================================================

# On se connecte a elexir
my $dbh = DBI->connect(
	'DBI:mysql:' . $db_name . ':' . $db_host,
	$db_user,
	$db_pass
);

# On récupère les infos de l'analyse
my $infos_analyse = get_infos_analyse($dbh, $id_analyse);

# Message d'erreur si l'analyse n'existe pas
die('L\'id de l\'analyse n\'est pas valide.') if(!$infos_analyse);

# petit raccourcit...
my $paire = ($infos_analyse->{'paired'});

# ==============================================================================
# On crée les noms de table dont on a besoin
# ==============================================================================

my $table_entites = $config->{'table_entites'}{$infos_analyse->{'version'}}{$infos_analyse->{'organism'}};
my $table_sondes = $config->{'table_sondes'}{$infos_analyse->{'type_chips'}}{$infos_analyse->{'version'}}{$infos_analyse->{'organism'}};
my $table_dabg = get_table_dabg($infos_analyse->{'id_project'});
my $table_intensites = get_table_intensites($infos_analyse->{'id_project'});
my $table_trans = get_table_transcription($infos_analyse->{'id_project'}, $id_analyse);
my $table_si = get_table_si($infos_analyse->{'id_project'}, $id_analyse);
my $table_splicing = get_table_splicing($infos_analyse->{'id_project'}, $id_analyse);

# ==============================================================================
# On prépare les requete sur elexir pour l'analyse avec ces infos
# ==============================================================================

# On selectionne les gènes qui ont un fc épissage
my $select_genes_sth = $dbh->prepare(
	"SELECT * FROM " . $table_trans
);

# On selectionne les entités codantes d'un gène
my $select_entites_sth = $dbh->prepare(
	"SELECT id, type, exon_pos FROM " . $table_entites . "
	WHERE id_gene = ? AND type != 'intron'"
);

# On selectionne les sondes d'un gène
my $select_infos_sondes_sth = $dbh->prepare(
	"SELECT probe_id, nb_occ, gc_content, exon_pos, entite_id, entite_type
	FROM " . $table_sondes . "
	WHERE entite_id = ?
	AND utiliser_dans_analyse = 1"
);

# On selectionne le dabg d'une sonde
my $select_dabg_sth = $dbh->prepare(
	"SELECT * FROM " . $table_dabg . " WHERE probe_id = ?"
);

# On selectionne l'intensité d'une sonde
my $select_intensite_sth = $dbh->prepare(
	"SELECT * FROM " . $table_intensites . " WHERE probe_id = ?"
);

# ==============================================================================
# On crée dynamiquement la table
# ==============================================================================

my @fields_SIs = ();

for(my $i = 0; $i < @{$infos_analyse->{'design'}}; $i++){

	push(@fields_SIs, 'SI' . ($i + 1));

}

my @fields_splicing = ();

push(@fields_splicing, 'id_entite INT UNSIGNED NOT NULL PRIMARY KEY');
push(@fields_splicing, 'gene_id INT UNSIGNED NOT NULL');
push(@fields_splicing, 'entite_type ENUM(\'exon\',\'prom\',\'polya\',\'donor\',\'acceptor\',\'deletion\',\'intron-retention\',\'intron\') NOT NULL');
push(@fields_splicing, 'entite_pos SMALLINT UNSIGNED NOT NULL');
push(@fields_splicing, (map { $_ . ' DECIMAL(10,5) UNSIGNED NOT NULL' } @fields_SIs)) if($paire);
push(@fields_splicing, 'SI DECIMAL(10,5) UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_pval DECIMAL(12,10) UNSIGNED NOT NULL');
push(@fields_splicing, 'nb_sondes SMALLINT UNSIGNED NOT NULL');
push(@fields_splicing, 'is_robust TINYINT UNSIGNED NOT NULL');

$dbh->do('DROP TABLE IF EXISTS ' . $table_splicing);
$dbh->do('CREATE TABLE ' . $table_splicing . ' (' . join(', ', @fields_splicing) . ')');

my $insert_entite_sth = $dbh->prepare(
	"INSERT INTO $table_splicing VALUES(" . join(', ', ('?')x@fields_splicing) . ")"
);

# ==============================================================================
# Début de l'algo, on analyse les gènes avec un fc épissage
# ==============================================================================

print 'Analyse des entités' . "\n";

# On selectionne les gènes
$select_genes_sth->execute;

# Pour chaque gène
while(my $gene = $select_genes_sth->fetchrow_hashref){

	# ======================================================================
	# On calcule les entités du gène
	# ======================================================================

	my @entites_gene = ();

	# On selectionne les entités du gène
	$select_entites_sth->execute($gene->{'gene_id'});

	# Pour chaque entité du gène
	while(my $entite = $select_entites_sth->fetchrow_hashref){

		print $gene->{'gene_id'} . ' ' . $entite->{'id'} . "\n";

		# ==============================================================
		# On selectionne les sondes
		# ==============================================================

		# On récupère les infos des sondes de l'entité
		$select_infos_sondes_sth->execute($entite->{'id'});
		my $infos_sondes = $select_infos_sondes_sth->fetchall_hashref('probe_id');
		$select_infos_sondes_sth->finish;

		# Si on a pas 3 sondes on zappe
		next if(scalar(keys %{$infos_sondes}) < 3);

		# On vire les sondes qui ont pas les bonnes caractéristiques
		my $ids_sondes_ok = [grep {
			$infos_sondes->{$_}->{'nb_occ'} <= $nb_occ_max
			and
			$infos_sondes->{$_}->{'gc_content'} <= $gc_max
		} keys %{$infos_sondes}];

		# Si on a pas 3 sondes on zappe
		next if(@{$ids_sondes_ok} < 3);

		# On récupère les SIs des sondes exprimés
		my @SIs_sondes = ();

		# Pour chaque id de sonde
		foreach my $id_sonde (@{$ids_sondes_ok}){

			# On récupère les valeurs de dabg de la sonde
			$select_dabg_sth->execute($id_sonde);
			my $dabg = $select_dabg_sth->fetchrow_hashref;
			$select_dabg_sth->finish;

			my $dabg_ba = dabg(
				$infos_analyse->{'design'}->[0]->{'control'},
				$seuil_dabg,
				$dabg
			);

			my $dabg_dc = dabg(
				$infos_analyse->{'design'}->[0]->{'test'},
				$seuil_dabg,
				$dabg
			);

			# Si la sonde est exprimé
			if($dabg_ba || $dabg_dc){

				# On va chercher ses intensites
				$select_intensite_sth->execute($id_sonde);
				my $sonde = $select_intensite_sth->fetchrow_hashref;
				$select_intensite_sth->finish;

				# On calcule les SIs de la sonde
				my @SIs_sonde = sis_sonde($infos_analyse->{'design'}, $gene->{'fc_oj'}, $sonde);

				# Et on les ajoute à la liste des SIs
				push(@SIs_sondes, \@SIs_sonde);

			}

		}

		# Si il n'y a pas trois sondes exprimées on zappe
		next if(@SIs_sondes < 3);

		# ==============================================================
		# On calcule le SI de l'entité (ça fait chier la c diff pour paire et impaire)
		# ==============================================================

		my $SI;
		my @SIs = ();
		my @SIs_a_tester = ();
		my $p_value;

		# => On pourra condenser quand on fera base 10 partout

		# La seule diff c'est que pour np on test avec tout les
		# si des sondes, avec p on test avec les si des paires
		# de replicats
		# Le reste pourrais être un seul algo

		# Pour chaque paires de replicat on récupère son SI et les SIs
		# à tester
		for(my $i = 0; $i < @{$infos_analyse->{'design'}}; $i++){

			# On récupère les SIs du réplicat 
			my @SIs_paire_replicat = map { @{$_}[$i + 1] } @SIs_sondes;

			# On calcule le SI du réplicat
			my $SI_paire_replicat = median(@SIs_paire_replicat);

			# On l'ajoute a la liste des SIs de l'entité
			push(@SIs, $SI_paire_replicat);

			# Selon que l'experience est paire ou non on met pas
			# les mêmes valeurs dans la liste des SIs a tester
			if($paire){

				push(@SIs_a_tester, $SI_paire_replicat);

			}else{

				push(@SIs_a_tester, @SIs_paire_replicat);

			}

		}

		# On calcule le si de l'entité
		$SI = median(@SIs);

		# On calcule la pvalue
		$p_value = ttest((log2($SI) >= 0), log2(@SIs_a_tester));

		# ==============================================================
		# On calcule si l'entité est "cohérente"
		# ==============================================================

		# On récupère le nombre de sondes de l'entité
		my $nb_sondes = @SIs_sondes;

		# Pour chaque sonde on calcule la médiane de ses si
		# et on compte le nombre de sondes up et de sondes down
		my $nb_ups = 0;
		my $nb_downs = 0;

		# Pour chaque sondes
		foreach my $SIs_sonde (@SIs_sondes){

			# On calcule le SI de la sonde
			my $si_sonde = median(@{$SIs_sonde});

			# Si la sonde est up ou down on la compte
			if($si_sonde >= $seuil_si){ $nb_ups++; }
			if((1/$si_sonde) >= $seuil_si){ $nb_downs++; }

		}

		# On récupère la limite
		my $limit = int($nb_sondes*3/4);

		# On regarde si l'entité est globalement up ou down
		my $is_up = ($nb_ups >= $limit);
		my $is_down = ($nb_downs >= $limit);

		# Si l'entité est up ou down, elle est robuste
		my $is_robust = ($is_up or $is_down);

		# ==============================================================
		# On ajoute l'entité à la liste des entites
		# ==============================================================

		my @values = ();

		push(@values, $entite->{'id'});
		push(@values, $gene->{'id_gene'});
		push(@values, $entite->{'type'});
		push(@values, $entite->{'exon_pos'});
		push(@values, @SIs) if($paire);
		push(@values, $SI);
		push(@values, $p_value);
		push(@values, $nb_sondes);
		push(@values, $is_robust);

		$insert_entite_sth->execute(@values);

	}

	# On termine la requète pour les entités
	$select_entites_sth->finish;

}
