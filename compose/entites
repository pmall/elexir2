#!/usr/bin/perl
use strict;
use warnings;
use YAML::XS qw(LoadFile);
use DBI;
use List::MoreUtils qw(uniq);
use FindBin qw($Bin);
use lib $FindBin::Bin . '/../lib';
use Format;
use Math;
use Analyse;
use Stats;

# ==============================================================================
# Parametres
# ==============================================================================

# On récupère l'id du projet a analyser
my $id_analyse = shift @ARGV || die('Vous devez passer l\'id d\'une analyse en paramètre');

# On récupère un hash de config
my $config = LoadFile($FindBin::Bin . '/../config.yml');

# On défini les identifiants pour la bdd
my $db_host = $config->{'db_host'};
my $db_user = $config->{'db_user'};
my $db_pass = $config->{'db_pass'};
my $db_name = $config->{'db_name'};

# Parametres pour l'analyse
my $gc_max = $config->{'compose'}{'gc_max'};
my $nb_occ_max = $config->{'compose'}{'nb_occ_max'};
my $seuil_dabg = $config->{'compose'}{'seuil_dabg'};
my $nb_sondes_min_entite = $config->{'compose'}{'nb_sondes_min_entite'};
my $seuil_si_robust = $config->{'compose'}{'seuil_si_robust'};
my $seuil_percent_robust = $config->{'simple'}{'seuil_percent_robust'};
my $seuil_nb_sondes_min_robust = $config->{'simple'}{'seuil_nb_sondes_min_robust'};

# ==============================================================================
# Préparation des requètes pour les infos du projet
# ==============================================================================

# On se connecte a elexir
my $dbh = DBI->connect(
	'DBI:mysql:' . $db_name . ':' . $db_host,
	$db_user,
	$db_pass
);

# On récupère les infos de l'analyse
my $infos_analyse = get_infos_analyse($dbh, $id_analyse);

# Message d'erreur si l'analyse n'existe pas
die('L\'id de l\'analyse n\'est pas valide.') if(!$infos_analyse);

# Message d'erreur si c'est pas le bon script pour le bon type d'analyse
if($infos_analyse->{'type'} ne 'compose'){

	die('Le script composé ne fonctionne que sur les analyses composées :o)');

}

# petit raccourcit...
my $paire = $infos_analyse->{'paired'};

# ==============================================================================
# On crée les noms de table dont on a besoin
# ==============================================================================

my $table_entites = $config->{'table_entites'}{$infos_analyse->{'version'}}{$infos_analyse->{'organism'}};
my $table_sondes = $config->{'table_sondes'}{$infos_analyse->{'type_chips'}}{$infos_analyse->{'version'}}{$infos_analyse->{'organism'}};
my $table_dabg = get_table_dabg($infos_analyse->{'id_project'});
my $table_intensites = get_table_intensites($infos_analyse->{'id_project'});
my $table_trans = get_table_transcription($infos_analyse->{'id_project'}, $id_analyse);
my $table_si = get_table_si($infos_analyse->{'id_project'}, $id_analyse);
my $table_splicing = get_table_splicing($infos_analyse->{'id_project'}, $id_analyse);

# ==============================================================================
# On prépare les requete sur elexir pour l'analyse avec ces infos
# ==============================================================================

# On selectionne les gènes qui ont un fc épissage
my $select_genes_sth = $dbh->prepare(
	"SELECT * FROM " . $table_trans
);

# On selectionne les entités codantes d'un gène
my $select_entites_sth = $dbh->prepare(
	"SELECT id, type, exon_pos FROM " . $table_entites . "
	WHERE id_gene = ? AND type != 'intron'"
);

# On selectionne les sondes d'un gène
my $select_infos_sondes_sth = $dbh->prepare(
	"SELECT probe_id, nb_occ, gc_content, exon_pos, entite_id, entite_type
	FROM " . $table_sondes . "
	WHERE entite_id = ?
	AND utiliser_dans_analyse = 1"
);

# On selectionne le dabg d'une sonde
my $select_dabg_sth = $dbh->prepare(
	"SELECT * FROM " . $table_dabg . " WHERE probe_id = ?"
);

# On selectionne l'intensité d'une sonde
my $select_intensite_sth = $dbh->prepare(
	"SELECT * FROM " . $table_intensites . " WHERE probe_id = ?"
);

# ==============================================================================
# On crée dynamiquement la table
# ==============================================================================

my @fields_splicing = ();

push(@fields_splicing, 'id_entite INT UNSIGNED NOT NULL PRIMARY KEY');
push(@fields_splicing, 'gene_id INT UNSIGNED NOT NULL');
push(@fields_splicing, 'entite_type ENUM(\'exon\',\'prom\',\'polya\',\'donor\',\'acceptor\',\'deletion\',\'intron-retention\',\'intron\') NOT NULL');
push(@fields_splicing, 'entite_pos SMALLINT UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_ba DECIMAL(10,5) UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_dc DECIMAL(10,5) UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_oj DECIMAL(10,5) UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_pval_ba DECIMAL(12,10) UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_pval_dc DECIMAL(12,10) UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_pval_oj DECIMAL(12,10) UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_adjp_ba DECIMAL(12,10) UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_adjp_dc DECIMAL(12,10) UNSIGNED NOT NULL');
push(@fields_splicing, 'SI_adjp_oj DECIMAL(12,10) UNSIGNED NOT NULL');
push(@fields_splicing, 'nb_sondes_ba SMALLINT UNSIGNED NOT NULL');
push(@fields_splicing, 'nb_sondes_dc SMALLINT UNSIGNED NOT NULL');
push(@fields_splicing, 'nb_sondes_oj SMALLINT UNSIGNED NOT NULL');
push(@fields_splicing, 'is_robust_ba TINYINT UNSIGNED NOT NULL');
push(@fields_splicing, 'is_robust_dc TINYINT UNSIGNED NOT NULL');
push(@fields_splicing, 'is_robust_oj TINYINT UNSIGNED NOT NULL');

$dbh->do('DROP TABLE IF EXISTS ' . $table_splicing);
$dbh->do('CREATE TABLE ' . $table_splicing . ' (' . join(', ', @fields_splicing) . ')');

my $insert_entite_sth = $dbh->prepare(
	"INSERT INTO $table_splicing VALUES(" . join(', ', ('?')x@fields_splicing) . ")"
);

# ==============================================================================
# Début de l'algo, on analyse les gènes avec un fc épissage
# ==============================================================================

print 'Analyse des entités' . "\n";

# On défini une liste d'entité
my @entites = ();

# On selectionne les gènes
$select_genes_sth->execute;

# Pour chaque gène
while(my $gene = $select_genes_sth->fetchrow_hashref){

	# ======================================================================
	# On calcule les entités du gène
	# ======================================================================

	# On selectionne les entités du gène
	$select_entites_sth->execute($gene->{'gene_id'});

	# Pour chaque entité du gène
	while(my $entite = $select_entites_sth->fetchrow_hashref){

		print $gene->{'gene_id'} . ' ' . $entite->{'id'} . "\n";

		# ==============================================================
		# On selectionne les sondes
		# ==============================================================

		# On récupère les infos des sondes de l'entité
		$select_infos_sondes_sth->execute($entite->{'id'});
		my $infos_sondes = $select_infos_sondes_sth->fetchall_hashref('probe_id');
		$select_infos_sondes_sth->finish;

		# Si on a pas 3 sondes on zappe
		next if(scalar(keys %{$infos_sondes}) < $nb_sondes_min_entite);

		# On vire les sondes qui ont pas les bonnes caractéristiques
		my $ids_sondes_ok = [grep {
			# ON VIRE LES SONDES AVEC UN GC <= 2 PARCE QU'ON A PAS PU
			# CALCULER LEUR DABG AVEC APT :((
			$infos_sondes->{$_}->{'gc_content'} > 2
			and
			$infos_sondes->{$_}->{'nb_occ'} <= $nb_occ_max
			and
			$infos_sondes->{$_}->{'gc_content'} <= $gc_max
		} keys %{$infos_sondes}];

		# Si on a pas 3 sondes on zappe
		next if(@{$ids_sondes_ok} < $nb_sondes_min_entite);

		# On récupère les SIs des sondes exprimés
		my @sondes_ba = ();
		my @sondes_dc = ();
		my @sondes_oj = ();

		# Pour chaque id de sonde
		foreach my $id_sonde (@{$ids_sondes_ok}){

			# On récupère les valeurs de dabg de la sonde
			$select_dabg_sth->execute($id_sonde);
			my $dabg = $select_dabg_sth->fetchrow_hashref;
			$select_dabg_sth->finish;

			my $dabg_ba = dabg(
				$infos_analyse->{'design'}->[0]->{'control'},
				$dabg,
				$seuil_dabg
			);

			my $dabg_dc = dabg(
				$infos_analyse->{'design'}->[0]->{'test'},
				$dabg,
				$seuil_dabg
			);

			# Si la sonde est exprimé
			if($dabg_ba || $dabg_dc){

				# On va chercher ses intensites
				$select_intensite_sth->execute($id_sonde);
				my $sonde = $select_intensite_sth->fetchrow_hashref;
				$select_intensite_sth->finish;

				# Et on les ajoute à la liste des SIs
				push(@sondes_ba, $sonde) if($dabg_ba);
				push(@sondes_dc, $sonde) if($dabg_dc);
				push(@sondes_oj, $sonde) if($dabg_ba && $dabg_dc);

			}

		}

		# Si il n'y a pas trois sondes exprimées on zappe
		next if(@sondes_ba < $nb_sondes_min_entite);
		next if(@sondes_dc < $nb_sondes_min_entite);
		next if(@sondes_oj < $nb_sondes_min_entite);

		# ==============================================================
		# On calcule les SIs des sondes
		# ==============================================================

		# Comparaison B/A
		my @SIs_sondes_ba = sis_sondes(
			$infos_analyse->{'design'}->[0]->{'control'},
			$gene->{'fc_ba'},
			\@sondes_ba
		);

		# Comparaison D/C
		my @SIs_sondes_dc = sis_sondes(
			$infos_analyse->{'design'}->[0]->{'test'},
			$gene->{'fc_dc'},
			\@sondes_dc
		);

		# Comparaison O/J
		my @SIs_sondes_oj = sis_sondes(
			$infos_analyse->{'design'},
			$gene->{'fc_oj'},
			\@sondes_oj
		);

		# ==============================================================
		# On calcule le SI de l'entité
		# ==============================================================

		# Comparaison B/A
		my($ref_SIs_ba, $SI_ba, $p_value_ba) = si_entite(
			$infos_analyse->{'design'}->[0]->{'control'},
			\@SIs_sondes_ba,
			$paire
		);

		# Comparaison D/C
		my($ref_SIs_dc, $SI_dc, $p_value_dc) = si_entite(
			$infos_analyse->{'design'}->[0]->{'test'},
			\@SIs_sondes_dc,
			$paire
		);


		# Comparaison O/J
		my($ref_SIs_oj, $SI_oj, $p_value_oj) = si_entite(
			$infos_analyse->{'design'},
			\@SIs_sondes_oj,
			$paire
		);

		# ==============================================================
		# On calcule si l'entité est "cohérente"
		# ==============================================================

		my $is_robust_ba = is_robust(
			[map { median(@{$_}) } @SIs_sondes_ba],
			$seuil_si_robust,
			$seuil_percent_robust,
			$seuil_nb_sondes_min_robust
		);

		my $is_robust_dc = is_robust(
			[map { median(@{$_}) } @SIs_sondes_dc],
			$seuil_si_robust,
			$seuil_percent_robust,
			$seuil_nb_sondes_min_robust
		);

		my $is_robust_oj = is_robust(
			[map { median(@{$_}) } @SIs_sondes_oj],
			$seuil_si_robust,
			$seuil_percent_robust,
			$seuil_nb_sondes_min_robust
		);

		# ==============================================================
		# On ajoute l'entité à la liste des entites
		# ==============================================================

		push(@entites, {
			'entite_id' => $entite->{'id'},
			'gene_id' => $gene->{'gene_id'},
			'type' => $entite->{'type'},
			'exon_pos' => $entite->{'exon_pos'},
			'SIs_ba' => $ref_SIs_ba,
			'SI_ba' => $SI_ba,
			'SIs_dc' => $ref_SIs_dc,
			'SI_dc' => $SI_dc,
			'SIs_oj' => $ref_SIs_oj,
			'SI_oj' => $SI_oj,
			'p_value_ba' => $p_value_ba,
			'p_value_dc' => $p_value_dc,
			'p_value_oj' => $p_value_oj,
			'nb_sondes_ba' => scalar(@sondes_ba),
			'nb_sondes_dc' => scalar(@sondes_dc),
			'nb_sondes_oj' => scalar(@sondes_oj),
			'is_robust_ba' => $is_robust_ba,
			'is_robust_dc' => $is_robust_dc,
			'is_robust_oj' => $is_robust_oj,
		});

	}

	# On termine la requète pour les entités
	$select_entites_sth->finish;

}

# On calcule les pvalues ajustées
my @adjp_values_ba = adjust_pvals(map { $_->{'p_value_ba'} } @entites);
my @adjp_values_dc = adjust_pvals(map { $_->{'p_value_dc'} } @entites);
my @adjp_values_oj = adjust_pvals(map { $_->{'p_value_oj'} } @entites);

# On ajoute les entités
for(my $i = 0; $i < @entites; $i++){

	my @values = ();

	push(@values, $entites[$i]->{'entite_id'});
	push(@values, $entites[$i]->{'gene_id'});
	push(@values, $entites[$i]->{'type'});
	push(@values, $entites[$i]->{'exon_pos'});
	push(@values, $entites[$i]->{'SI_ba'});
	push(@values, $entites[$i]->{'SI_dc'});
	push(@values, $entites[$i]->{'SI_oj'});
	push(@values, $entites[$i]->{'p_value_ba'});
	push(@values, $entites[$i]->{'p_value_dc'});
	push(@values, $entites[$i]->{'p_value_oj'});
	push(@values, $adjp_values_ba[$i]);
	push(@values, $adjp_values_dc[$i]);
	push(@values, $adjp_values_oj[$i]);
	push(@values, $entites[$i]->{'nb_sondes_ba'});
	push(@values, $entites[$i]->{'nb_sondes_dc'});
	push(@values, $entites[$i]->{'nb_sondes_oj'});
	push(@values, $entites[$i]->{'is_robust_ba'});
	push(@values, $entites[$i]->{'is_robust_dc'});
	push(@values, $entites[$i]->{'is_robust_oj'});

	$insert_entite_sth->execute(@values);

}
