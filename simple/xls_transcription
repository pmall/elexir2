#!/usr/bin/perl
use warnings;
use strict;
use YAML::XS qw(LoadFile);
use DBI;
use File::Path qw(make_path);
use Spreadsheet::WriteExcel;
use FindBin qw($Bin);
use lib "$FindBin::Bin/../lib";
use Format;
use Math;
use Analyse;

# ==============================================================================
# Parametres
# ==============================================================================

# On récupère l'id du projet a analyser
my $id_analyse = shift @ARGV || die('Vous devez passer l\'id d\'une analyse en paramètre');

# On récupère un hash de config
my $config = LoadFile($FindBin::Bin . '/../config.yml');

# On défini les identifiants pour la bdd
my $db_host = $config->{'db_host'};
my $db_user = $config->{'db_user'};
my $db_pass = $config->{'db_pass'};
my $db_name = $config->{'db_name'};

# Paramètres de sélection pour les résulats
my $seuil_trans = $config->{'simple'}{'excel'}{'seuil_trans'};
my $seuil_pval = $config->{'simple'}{'excel'}{'seuil_pval'};
my $seuil_fiabilite_fcGene = $config->{'simple'}{'excel'}{'seuil_fc_gene'};

# Répertoire de sortie 
my $dir_xls = $config->{'dir_xls'};

# ==============================================================================
# On récupère les infos de l'analyse
# ==============================================================================

my $dbh = DBI->connect(
	'DBI:mysql:' . $db_name . ':' . $db_host,
	$db_user,
	$db_pass
);

my $analyse = Analyse->get_analyse($dbh, $id_analyse);

die('L\'id de l\'analyse n\'est pas valide.') if(!$analyse);

# Message d'erreur si c'est pas le bon script pour le bon type d'analyse
if($analyse->{'type'} ne 'simple'){

	die('Le script simple ne fonctionne que sur les analyses simples :o)');

}

# ==============================================================================
# On récupère les infos dont on a besoin selon les infos de l'analyse
# ==============================================================================

# On récupère les infos de la bdd
my $id_projet = $analyse->{"id_project"};
my $type_chips = $analyse->{'type_chips'};
my $version = $analyse->{'version'};
my $orga = $analyse->{'organism'};
my $name = $analyse->{'name'};

# on crée les variables du script qui déendent de l'analyse
my $table_genes = $config->{'table_genes'}{$version}{$orga};
my $table_trans = get_table_transcription($id_projet, $id_analyse);
my $dir_sortie = $dir_xls . '/' . $id_projet . '/' . $id_analyse;
my $fichier_sortie = $dir_sortie . '/' . $name . '_transcription.xls';

# On crée le répertoire de sortie au cas ou il existe pas
make_path($dir_sortie);

# ==============================================================================
# On se connecte a fasterdb et on prépare les requetes
# ==============================================================================

my $select_genes_nofdr_sth = $dbh->prepare(
	"SELECT id_gene, trans_fc, trans_pval, trans_nb_sondes, trans_nb_exons
	FROM $table_trans
	WHERE ABS(LOG2(trans_fc)) >= LOG2(?) AND trans_pval <= ?"
);

my $select_genes_fdr_sth = $dbh->prepare(
	"SELECT id_gene, trans_fc, trans_adjp AS trans_pval, trans_nb_sondes, trans_nb_exons
	FROM $table_trans
	WHERE ABS(LOG2(trans_fc)) >= LOG2(?) AND trans_adjp <= ?"
);

my $select_infos_gene_sth = $dbh->prepare(
	"SELECT * FROM $table_genes WHERE id_fasterdb = ?"
);

# ==============================================================================
# Initialisation fichier excel
# ==============================================================================

# On crée un fichier excel
my $xlsh = Spreadsheet::WriteExcel->new($fichier_sortie);

# On ajoute les pages
my $f_resume			= $xlsh->add_worksheet('Resume de l\'analyse');
my $f_transcription_no_fdr	= $xlsh->add_worksheet('SANS correction pvals');
my $f_transcription_fdr		= $xlsh->add_worksheet('AVEC correction pvals');

# On défini les couleurs
my $couleur_entete		= $xlsh->set_custom_color(39, 189, 189, 189);
my $couleur_up			= $xlsh->set_custom_color(40, 255, 204, 153);
my $couleur_down		= $xlsh->set_custom_color(41, 204, 255, 204);

# On définit les format d'affichage
my $format_resume_titre		= $xlsh->add_format(color => 'black', bold => 1, center_across => 1);
my $format_cell_center		= $xlsh->add_format(color => 'black', bold => 0, center_across => 1);
my $format_cell_no_center	= $xlsh->add_format(color => 'black', bold => 0);
my $format_merge_entete		= $xlsh->add_format(color => 'black', bold => 1, bg_color => $couleur_entete, border => 2, border_color => 'black', center_across => 1);
my $format_entete		= $xlsh->add_format(color => 'black', bold => 1, bg_color => $couleur_entete, border => 2, border_color => 'black', center_across => 1);
my $format_up			= $xlsh->add_format(color => 'black', bg_color => $couleur_up, border => 1, border_color => 'black', center_across => 1);
my $format_down			= $xlsh->add_format(color => 'black', bg_color => $couleur_down, border => 1, border_color => 'black', center_across => 1);

# ==============================================================================
# Feuille résumé
# ==============================================================================

# Infos de base
$f_resume->write(0, 0, date(), $format_cell_no_center);
$f_resume->write(2, 0, "Parametres utilises", $format_resume_titre);
$f_resume->write(3, 0, "Seuil fold", $format_resume_titre);
$f_resume->write(3, 1, round($seuil_trans, 2), $format_resume_titre);
$f_resume->write(4, 0, "Seuil p-value", $format_resume_titre);
$f_resume->write(4, 1, $seuil_pval, $format_resume_titre);

# Infos sur les sondes
my $num_ligne = 8;
$f_resume->set_column(0, 0, 25);
$f_resume->set_column(2, 2, 20);
$f_resume->write($num_ligne++, 0, "Sondes et entites analysees", $format_cell_center);
$num_ligne++;
$f_resume->write($num_ligne++, 0, "Sondes", $format_cell_center);
$num_ligne++;
$f_resume->write($num_ligne++, 0, "Sur la puce", $format_cell_center);
$f_resume->write($num_ligne++, 0, "Avec gc < 18", $format_cell_center);
$f_resume->write($num_ligne++, 0, "Non ch", $format_cell_center);
$f_resume->write($num_ligne++, 0, "Exprimees", $format_cell_center);
$num_ligne++;
$f_resume->write($num_ligne++, 0, "Ciblant des entites", $format_cell_center);
$f_resume->write($num_ligne++, 0, "Introniques", $format_cell_center);
$f_resume->write($num_ligne++, 0, "Non utilisees (inter-geniques, chevauchantes, non hybridees...)", $format_cell_no_center);
$num_ligne++;
$f_resume->write($num_ligne++, 0, "Analysables*", $format_cell_center);
$f_resume->write($num_ligne++, 0, "Analysees*", $format_cell_center);

# Légende
$f_resume->write(36, 0, "* analysable", $format_cell_no_center);
$f_resume->write(36, 1, "avec sonde : gc < 18, non ch et ciblant une entite", $format_cell_no_center);
$f_resume->write(37, 0, "* analysable", $format_cell_no_center);
$f_resume->write(37, 1, "avec sonde : gc <18, non ch, ciblant une entite et exprimee dans l'experience", $format_cell_no_center);

# ==============================================================================
# On ajoute les gènes dans les feuilles
# ==============================================================================

my @genes = ();

# On selectionne les gènes no fdr
$select_genes_nofdr_sth->execute($seuil_trans, $seuil_pval);
@genes = @{$select_genes_nofdr_sth->fetchall_arrayref({})};
$select_genes_nofdr_sth->finish;

my($nb_nofdr_up, $nb_nofdr_down) = write_list($f_transcription_no_fdr, @genes);

# On selectionne les gènes fdr
$select_genes_fdr_sth->execute($seuil_trans, $seuil_pval);
@genes = @{$select_genes_fdr_sth->fetchall_arrayref({})};
$select_genes_fdr_sth->finish;

my($nb_fdr_up, $nb_fdr_down) = write_list($f_transcription_fdr,	@genes);

# ==============================================================================
# Fonction pour remplir la feuille
# ==============================================================================

sub write_list{

	my($f, @genes) = @_;

	my $nb_up = 0;
	my $nb_down = 0;

	# On fait la première entete
	$f->merge_range(0, 0, 0, 7, 'Infos gene', $format_merge_entete);
	$f->merge_range(0, 8, 0, 10, 'Reg gene', $format_merge_entete);

	# On modifie la largeur des colones
	$f->set_column(0, 0, 12); # Fasterdb id
	$f->set_column(1, 1, 18); # Source id
	$f->set_column(2, 2, 12); # Link
	$f->set_column(3, 3, 12); # Symbol
	$f->set_column(4, 4, 30); # Description
	$f->set_column(5, 5, 24); # Pos chrom
	$f->set_column(6, 6, 12); # Regulation
	$f->set_column(7, 7, 8); # FC
	$f->set_column(8, 8, 12); # pval
	$f->set_column(9, 9, 12); # nb probes

	# On défini les entetes
	my @entetes = (
		'Fasterdb id',
		'Source id',
		'Link Elexir',
		'Symbol',
		'Description',
		'Chr Position',
		'Nb probes',
		'Nb exons',
		'Regulation',
		'FC',
		'Pvalue'
	);

	for(my $j = 0; $j < @entetes; $j++){

		$f->write(1, $j, $entetes[$j], $format_entete);

	}

	# On classe les gènes
	@genes = sort {
		# On classe déjà les up en premier
		($b->{'trans_fc'} >= 1) <=> ($a->{'trans_fc'} >= 1)
		||
		# Ensuite par fold
		format_fold($b->{'trans_fc'}) <=> format_fold($a->{'trans_fc'})
	} @genes;

	# Pour chaque gène
	for(my $i = 0; $i < @genes; $i++){

		my $gene = $genes[$i];

		# On récupère les infos du gène
		$select_infos_gene_sth->execute($gene->{'id_gene'});
		my $infos_gene = $select_infos_gene_sth->fetchrow_hashref;
		$select_infos_gene_sth->finish;

		# On compte les up et les down
		($gene->{'trans_fc'} >= 1) ? $nb_up++ : $nb_down++;

		# On formatte les infos
		$infos_gene->{'description'} =~ s/\[.+\]$//;

		my $pos_chrom =
			$infos_gene->{'chromosome'} .
			':' . $infos_gene->{'start_sur_chromosome'} .
			'-' . $infos_gene->{'end_sur_chromosome'} .
			':' . $infos_gene->{'strand'};

		my $regulation = ($gene->{'trans_fc'} >= 1) ? 'up' : 'down';

		my $format = ($gene->{'trans_fc'} >= 1) ? $format_up : $format_down;

		# On crée le tableau de data
		my @data = (
			$infos_gene->{'id_fasterdb'},
			$infos_gene->{'id_source'},
			'blah',
			$infos_gene->{'symbol'},
			$infos_gene->{'description'},
			$pos_chrom,
			$gene->{'trans_nb_sondes'},
			$gene->{'trans_nb_exons'},
			$regulation,
			round(format_fold($gene->{'trans_fc'}), 2),
			$gene->{'trans_pval'}
		);

		# On affiche les data
		for(my $j = 0; $j < @data; $j++){

			# $i + 1 parce que la premiere ligne est l'entete
			$f->write($i + 2, $j, $data[$j], $format);

		}

	}

	return($nb_up, $nb_down);

}

# ==============================================================================
# On écrit le nombre de gènes fdr up/down no fdr up/down
# ==============================================================================

$num_ligne = 8;

$f_resume->set_column(5, 5, 30);
$f_resume->set_column(9, 9, 13);
$f_resume->set_column(9, 9, 13);
$f_resume->set_column(11, 11, 18);

$f_resume->write($num_ligne++, 5, "Resultats", $format_resume_titre);
$num_ligne++;
$f_resume->write($num_ligne++, 5, "Sans correction des p-valeurs", $format_resume_titre);
$num_ligne++;
$f_resume->write($num_ligne, 5, "Genes regules", $format_cell_center);
$f_resume->write($num_ligne, 6, $nb_nofdr_up + $nb_nofdr_down, $format_cell_center);
$f_resume->write($num_ligne, 7, "Genes up", $format_cell_center);
$f_resume->write($num_ligne, 8, $nb_nofdr_up, $format_cell_center);
$f_resume->write($num_ligne, 9, "Genes down", $format_cell_center);
$f_resume->write($num_ligne, 10, $nb_nofdr_down, $format_cell_center);
$num_ligne++;
$num_ligne++;
$f_resume->write($num_ligne++, 5, "Avec correction des p-valeurs", $format_resume_titre);
$num_ligne++;
$f_resume->write($num_ligne, 5, "Genes regules", $format_cell_center);
$f_resume->write($num_ligne, 6, $nb_fdr_up + $nb_fdr_down, $format_cell_center);
$f_resume->write($num_ligne, 7, "Genes up", $format_cell_center);
$f_resume->write($num_ligne, 8, $nb_fdr_up, $format_cell_center);
$f_resume->write($num_ligne, 9, "Genes down", $format_cell_center);
$f_resume->write($num_ligne, 10, $nb_fdr_down, $format_cell_center);
