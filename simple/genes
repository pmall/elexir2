#!/usr/bin/perl
use strict;
use warnings;
use YAML::XS qw(LoadFile);
use DBI;
use List::MoreUtils qw(uniq);
use FindBin qw($Bin);
use lib $FindBin::Bin . '/../lib';
use Format;
use Math;
use Analyse;
use Stats;

# ==============================================================================
# Parametres
# ==============================================================================

# On récupère l'id du projet a analyser
my $id_analyse = shift @ARGV || die('Vous devez passer l\'id d\'une analyse en paramètre');
my $limit = shift @ARGV // 18446744073709551615; # Si pas indiqué, on met maxint /

# On récupère un hash de config
my $config = LoadFile($FindBin::Bin . '/../config.yml');

# On défini les identifiants pour la bdd
my $db_host = $config->{'db_host'};
my $db_user = $config->{'db_user'};
my $db_pass = $config->{'db_pass'};
my $db_name = $config->{'db_name'};

# Parametres pour l'analyse
my $gc_max = $config->{'simple'}{'gc_max'};
my $nb_occ_max = $config->{'simple'}{'nb_occ_max'};
my $seuil_dabg = $config->{'simple'}{'seuil_dabg'};

# ==============================================================================
# On prépare les requetes
# ==============================================================================

# On se connecte a elexir
my $dbh = DBI->connect(
	'DBI:mysql:' . $db_name . ':' . $db_host,
	$db_user,
	$db_pass
);

# On selectionne les infos de l'analyse
my $select_infos_analyse_sth = $dbh->prepare(
	"SELECT p.id AS id_project, p.type AS type_chips, p.organism, a.type
	FROM analyses AS a, projects AS p
	WHERE a.id_project = p.id
	AND a.id = ?"
);

# On selectionne les conditions du projet
my $select_chips_sth = $dbh->prepare(
	"SELECT g.letter, c.condition, c.num
	FROM analyses AS a, chips AS c, groups AS g
	WHERE a.id_project = c.id_project
	AND a.id = g.id_analysis
	AND c.condition = g.condition
	AND a.id = ?"
);

# ==============================================================================
# On teste si l'analyse existe bien
# ==============================================================================

$select_infos_analyse_sth->execute($id_analyse);

my $infos_analyse = $select_infos_analyse_sth->fetchrow_hashref;

die('L\'id de l\'analyse n\'est pas valide.') if(!$infos_analyse);

# ==============================================================================
# On crée les noms de table dont on a besoin
# ==============================================================================

my $base_fasterdb = $config->{'db_name_fasterdb'}{$infos_analyse->{'organism'}};
my $table_sondes = $config->{'table_sondes'}{$infos_analyse->{'organism'}}{$infos_analyse->{'type_chips'}};
my $table_dabg = get_table_dabg($infos_analyse->{'id_project'});
my $table_intensites = get_table_intensites($infos_analyse->{'id_project'});
my $table_trans = get_table_transcription($infos_analyse->{'id_project'}, $id_analyse);

# ==============================================================================
# On prépare les requetes sur la bonne base fasterdb
# ==============================================================================

# On se connecte a fasterdb
my $dbh_fdb = DBI->connect(
	'DBI:mysql:' . $base_fasterdb . ':' . $db_host,
	$db_user,
	$db_pass
);

# On selectionne les gènes
my $select_genes_sth = $dbh_fdb->prepare("SELECT id FROM genes LIMIT ?");

# On selectionne le nombre d'exons d'un gène
my $select_nb_exons_gene_sth = $dbh_fdb->prepare(
	"SELECT COUNT(id) AS nb_exons FROM exons_genomiques WHERE id_gene = ?"
);

# ==============================================================================
# On prépare la requete pour les sondes avec ces infos
# ==============================================================================

# On selectionne les sondes d'un gène
my $select_infos_sondes_sth = $dbh->prepare(
	"SELECT probe_id, nb_occ, gc_content, exon_pos
	FROM " . $table_sondes . "
	WHERE entite_type = 1
	AND utiliser_dans_analyse = 1
	AND gene_id = ?"
);

# On selectionne le dabg d'une sonde
my $select_dabg_sth = $dbh->prepare(
	"SELECT * FROM " . $table_dabg . " WHERE probe_id = ?"
);

# On selectionne l'intensité d'une sonde
my $select_intensite_sth = $dbh->prepare(
	"SELECT * FROM " . $table_intensites . " WHERE probe_id = ?"
);

# ==============================================================================
# On récupère la description de l'expérience
# ==============================================================================

my @exp = decrire(
	$select_chips_sth,
	$id_analyse,
	$infos_analyse->{'type'}
);

# ==============================================================================
# On crée la table qui contient les valeurs des gène
# ==============================================================================

my @fields_expr = ();

for(my $i = 1; $i <= @exp; $i++){

	push(@fields_expr, 'control' . $i);
	push(@fields_expr, 'test' . $i);

}

my @fields_trans = (
	'gene_id INT UNSIGNED NOT NULL PRIMARY KEY',
	(map { $_ . ' DECIMAL(10,5) UNSIGNED NOT NULL'; } @fields_expr),
	'trans_fc DECIMAL(10,5) NOT NULL',
	'trans_pval DECIMAL(12,10) UNSIGNED NOT NULL',
	'trans_adjp DECIMAL(12,10) UNSIGNED NOT NULL',
	'trans_nb_sondes SMALLINT UNSIGNED NOT NULL',
	'trans_nb_exons SMALLINT UNSIGNED NOT NULL',
	'epi_fc DECIMAL(10,5)',
	'epi_pval DECIMAL(12,5) UNSIGNED',
	'epi_adjp DECIMAL(12,5) UNSIGNED',
	'epi_nb_sondes SMALLINT UNSIGNED',
	'epi_nb_exons SMALLINT UNSIGNED'
);

$dbh->do('DROP TABLE IF EXISTS ' . $table_trans);
$dbh->do('CREATE TABLE ' . $table_trans . ' (' . join(', ', @fields_trans) . ')');

my $insert_gene_sth = $dbh->prepare(
	"INSERT INTO $table_trans VALUES(" . join(', ', ('?')x@fields_trans) . ")"
);

# ==============================================================================
# On début l'algo, pour chaque gène
# ==============================================================================

print 'Analyse des gènes' . "\n";

# On défini une liste de gènes (on les garde tous dans une liste pour calculer
# les pvals ajustés)
my @genes = ();

# On selectionne les gènes
$select_genes_sth->execute($limit);

# Pour chaque gène
while(my($id_gene) = $select_genes_sth->fetchrow_array){

	print $id_gene . "\n";

	# ======================================================================
	# On récupère les intensités des sondes du gène
	# ======================================================================

	# On récupère les infos des sondes
	$select_infos_sondes_sth->execute($id_gene);
	my $infos_sondes = $select_infos_sondes_sth->fetchall_hashref('probe_id');
	$select_infos_sondes_sth->finish;

	# Si le gène a moins de six sondes on zappe ça sert a rien
	next if(scalar(keys %{$infos_sondes}) < 6);

	# On selectionne les sondes qui ont les bonnes caractéristiques
	my $ids_probes_ok = [grep {
		$infos_sondes->{$_}->{'nb_occ'} <= $nb_occ_max
		and
		$infos_sondes->{$_}->{'gc_content'} <= $gc_max
	} keys %{$infos_sondes}];

	# Si le gène a moins de six sondes avec de bon critères on zappe
	next if(@{$ids_probes_ok} < 6);

	# On récupère les valeurs de dabg de chaque sonde
	my @sondes =();

	# Pour chaque id de sonde
	foreach my $probe_id (@{$ids_probes_ok}){

		# On récupère les valeurs de dabg de la sonde
		$select_dabg_sth->execute($probe_id);
		my $dabg = $select_dabg_sth->fetchrow_hashref;
		$select_dabg_sth->finish;

		# Si la sonde est exprimé
		if(dabg($seuil_dabg, \@exp, $dabg)){

			# On va chercher ses intensites
			$select_intensite_sth->execute($probe_id);
			my $sonde = $select_intensite_sth->fetchrow_hashref;
			$select_intensite_sth->finish;

			# Et on l'ajoute à la liste des sondes
			push(@sondes, $sonde);

		}

	}

	# Si il n'y a pas six sondes on zappe
	next if(@sondes < 6);

	# ======================================================================
	# Calcul de l'expression de gène
	# ======================================================================

	my @expr = expression(\@exp, \@sondes);

	# ======================================================================
	# Etape de lissage pour la transcription
	# ======================================================================

	# On selectionne les sondes qui passe le lissage transcription
	my @sondes_trans = lissage_transcription(\@exp, \@sondes);

	# Si il n'y a pas six sondes ça sert a rien de continuer pour ce gène
	next if(@sondes_trans < 6);

	# ======================================================================
	# Calcul des fold change et de la pvalue pour la transcription
	# ======================================================================

	# On calcul les fold change des sondes
	my @fcs_trans = fcs_sondes(\@exp, \@sondes_trans);

	# le fc du gène est la médiane des fc des sondes
	my $fc_gene_trans = median(@fcs_trans);

	# On calcule la p value pour la transcription
	my $p_value_trans = ttest(log2($fc_gene_trans), log2(@fcs_trans));

	# ======================================================================
	# Analyse au niveau de l'épissage
	# ======================================================================

	# On déclare les valeurs d'épissage
	my @sondes_epissage = ();
	my $fc_gene_epissage;
	my $p_value_epissage;

	# ======================================================================
	# Si le gene a au moins 4 exons on vérifie si au moins la moitié de ses
	# exons a au moins deux sondes
	# ======================================================================

	# Booléen pour dire si le gene passe ce critère
	my $homogene = 1;

	# On selectionne son nombre d'exon du gène
	$select_nb_exons_gene_sth->execute($id_gene);
	my($nb_exons) = $select_nb_exons_gene_sth->fetchrow_array;
	$select_nb_exons_gene_sth->finish;

	# Si le gène a au moins quatres exons
	if($nb_exons >= 4){

		# On compte le nombre de sondes par exon
		my %sondes_par_exons = ();

		foreach my $sonde (@sondes){

			$sondes_par_exons{$infos_sondes->{$sonde->{'probe_id'}}->{'exon_pos'}}++;

		}

		# On récupère le nombre d'exons qui sont ciblés par au moins
		# deux sondes
		my $nb_exons_2_sondes = grep {
			$sondes_par_exons{$_} > 1
		} keys %sondes_par_exons;

		# On met a jour le booleen homogene
		$homogene = ($nb_exons_2_sondes >= int($nb_exons/2));

	}

	# Si le gène est homogène on calcule les valeurs d'épissage
	if($homogene){

		# ==============================================================
		# Etape de lissage pour l'épissage
		# ==============================================================

		# On selectionne les sondes qui passe le lissage de l'épissage
		@sondes_epissage = lissage_epissage(\@exp, \@sondes);

		# Seulement si on a plus de 6 sondes épissage
		if(@sondes_epissage >= 6){

			# ======================================================
			# Calcul des fold change et de la pvalue pour l'épissage
			# ======================================================

			# On calcul les fold change des sondes epissage selon si l'analyse
			# est paire ou non
			my @fcs_epissage = fcs_sondes(\@exp, \@sondes_epissage);

			# le fc du gène est la médiane des fc des sondes
			$fc_gene_epissage = median(@fcs_epissage);

			# On calcule la p value pour l'épissage
			$p_value_epissage = ttest(log2($fc_gene_epissage), log2(@fcs_epissage));

		}

	}

	# ======================================================================
	# On calcule le nombre de sondes et d'exons utilisés dans l'analyse
	# ======================================================================

	# NB sondes utilisées pour la transcription
	my $nb_sondes_trans = @sondes_trans;

	# NB exons cibles par des sondes lors de la transcription
	my $nb_exons_trans = scalar(uniq(map { $infos_sondes->{$_->{'probe_id'}}->{exon_pos} } @sondes_trans));

	# NB sondes utilisées pour l'épissage
	my $nb_sondes_epissage = ($homogene == 1)
		? @sondes_epissage
		: undef;

	# NB exons cibles par des sondes lors de l'épissage
	my $nb_exons_epissage = ($homogene == 1)
		? scalar(uniq(map { $infos_sondes->{$_->{'probe_id'}}->{exon_pos} } @sondes_epissage))
		: undef;

	# ======================================================================
	# On ajoute les valeurs à la liste de gène
	# ======================================================================

	# On ajoute le gène à la liste
	push(@genes, {
		'id' => $id_gene,
		'expression' => \@expr,
		'fc_trans' => $fc_gene_trans,
		'p_value_trans' => $p_value_trans,
		'nb_sondes_trans' => $nb_sondes_trans,
		'nb_exons_trans' => $nb_exons_trans,
		'fc_epi' => $fc_gene_epissage,
		'p_value_epi' => $p_value_epissage,
		'nb_sondes_epi' => $nb_sondes_epissage,
		'nb_exons_epi' => $nb_exons_epissage
	});

}

# ==============================================================================
# On calcule les p values ajustées
# ==============================================================================

# On récupère la totalité des listes de pvalues trans et epi
my @pvals_trans = map { $_->{'p_value_trans'} } @genes;
my @pvals_epi = map { $_->{'p_value_epi'} } @genes;

# On les corrige
my @adjp_trans = adjust_pvals(\@pvals_trans);
my @adjp_epi = adjust_pvals(\@pvals_epi);

# Pour chaque gène
for(my $i = 0; $i < @genes; $i++){

	# On récupère le gènes
	my $gene = $genes[$i];

	# On insert le gène dans la bdd
	$insert_gene_sth->execute(
		$gene->{'id'},
		(map { log2($_) } (@{$gene->{'expression'}})),
		log2($gene->{'fc_trans'}),
		$gene->{'p_value_trans'},
		$adjp_trans[$i],
		$gene->{'nb_sondes_trans'},
		$gene->{'nb_exons_trans'},
		log2($gene->{'fc_epi'}),
		$gene->{'p_value_epi'},
		$adjp_epi[$i],
		$gene->{'nb_sondes_epi'},
		$gene->{'nb_exons_epi'}
	);

}
